apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    flavorDimensions "var", "env"

    defaultConfig {
        applicationId "jp.kyuuki.rensou.android"
        minSdkVersion 9
        targetSdkVersion 27
    }
    productFlavors {
        // 連想げーむ
        development {
            dimension "env"
            applicationIdSuffix '.development'
        }
        staging {
            dimension "env"
            applicationIdSuffix '.staging'
        }
        production {
            dimension "env"
        }

        // 連想げーむ
        rensou {
            dimension "var"
            versionCode 17
            versionName "1.1.8"
        }
        // しりとりげーむ
        shiritori {
            dimension "var"
            applicationId 'jp.kyuuki.shiritori.android'
            versionCode 2
            versionName "1.0.1"
        }
        // 乃木坂 No.1 決定戦
        nogizaka {
            dimension "var"
            applicationId 'jp.kyuuki.nogizaka.android'
            versionCode 1
            versionName "1.0.0"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // http://konifar.com/1405
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    // http://kokufu.blogspot.jp/2014/10/androidstudio-missingtranslation.html
    lintOptions {
        disable 'MissingTranslation'
        abortOnError false  // なぜか乃木坂リリース時に GradleException
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:23.0.0'
    //implementation 'com.google.android.gms:play-services:8.3.0'
    //implementation 'com.google.android.gms:play-services-analytics:8.4.0'  // 8.3.0 と競合とか言うエラーが出るので 8.3 に。Android Studio 2.0 にあげれば解決するかも
    implementation 'com.google.android.gms:play-services:8.4.0'
    implementation 'com.google.android.gms:play-services-analytics:8.4.0'
    implementation "com.google.android.gms:play-services-gcm:8.4.0"
    implementation 'com.google.android.gms:play-services-ads:8.4.0'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'commons-io:commons-io:2.4'
    //implementation 'commons-codec:commons-codec:1.10'
    implementation 'com.mcxiaoke.volley:library:1.0.19'
    // nend メディエーション用アダプター
    //implementation 'net.nend.android:nend-adapter:1.2.4'

    // ConstraintLayout
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
}

apply plugin: 'com.google.gms.google-services'
